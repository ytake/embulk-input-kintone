plugins {
    id "scala"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    provided
}

group = "net.jp.ytake"
version = "0.0.1"
description = "embulk input for kintone"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(ScalaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.19"
    compileOnly "org.embulk:embulk-spi:0.10.19"
    compile "org.scala-lang:scala-library:2.13.5"
    compile "com.google.code.gson:gson:2.8.6"
    compile "com.google.inject:guice:4.2.3"
    provided "com.google.inject:guice:4.2.3"

    compile("org.embulk:embulk-util-config:0.1.4") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "com.cybozu.kintone:kintone-sdk:0.5.0"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.10.19"
    testCompile "org.embulk:embulk-core:0.10.19:tests"
    testCompile "org.embulk:embulk-standards:0.10.19"
    testCompile "org.embulk:embulk-deps:0.10.19"
    testCompile group: 'org.scalatest', name: 'scalatest_2.13', version: '3.2.7'
    testCompile group: 'org.scalatestplus', name: 'junit-4-13_2.13', version: '3.2.7.0'
}

embulkPlugin {
    mainClass = "net.jp.ytake.embulk.input.kintone.KintoneInputPlugin"
    category = "input"
    type = "kintone"
}

test {
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

task scalatest(dependsOn: ['testClasses'], type: JavaExec) {
    main = 'org.scalatest.tools.Runner'
    args = ['-R', 'build/classes/scala/test', '-o']
    classpath = sourceSets.test.runtimeClasspath
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }
